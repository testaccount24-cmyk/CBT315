* VERSION 2
* THIS MACRO SIMPLY CONVERTS A BINARY NUMBER IN REG (&REG)
* TO AN UNPACKED DECIMAL FIELD (&DEST) FOR PRINTOUT.
* DEFINE: &DWORD  DS  D  - FOR USE BY ALL MACRO EXECUTIONS.
         MACRO
&NAME    CONVERT   &REG,&DEST,&DWORD
&NAME    CVD   &REG,&DWORD
         UNPK  &DEST,&DWORD
         OI    &DEST+L'&DEST-1,X'F0'
         MEND
         MACRO
         REGS
         PRINT OFF
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         PRINT ON
         MEND
*******************************************
* SUBMIT IS A GENERAL PURPOSE SUBROUTINE  *
* WHICH WILL DYNAMICALLY ALLOCATE THE JES *
* INTERNAL READER AND SUBMIT JCL ETC. FROM*
* A NATURAL PROGRAM. THE LAST CALL TO THIS*
* SUBROUTINE MUST BE A '/*EOF'.           *
* CALL 'TSOSUB' P1 P2                     *
*          P1 = CL80 TEXT TO SUBMIT       *
*          P2 = CL6 (CL2=RC,CL4=REASON)   *
* WRITTEN BY D.LEES 8/88                  *
* PROGRAM IS REUSEABLE; NATURAL LOADS A   *
* CALLED PROG AT 1ST CALL AND DELETES AT  *
* END OF NATURAL PGM (OR SO IT APPEARS).  *
*                                         *
* ERRORS: R15 CONTAINS RETURN CODE        *
*   RC    REASON   CAUSE                  *
*   __    ______   ____________________   *
*   NN    NNNN     ACTUAL R15 & S99ERROR  *
*   66    0000     WRITE TO INTRDR ERROR  *
*   77    0000     INTRDR OPEN ERROR      *
*******************************************
SUBMIT   CSECT                                                          00008
         PRINT NOGEN                                                    00036
         REGS                                                           00015
INIT     STM   R14,R12,12(R13)                                          00046
         BALR  R12,0                                                    00053
         USING *,R12                                                    00054
         LR    R11,R1                      PARMLIST
         LA    R1,SAVEAREA                                              00048
         ST    R13,4(R1)                                                00049
         ST    R1,8(R13)                                                00050
         LR    R13,R1                                                   00051
         LM    R10,R11,0(R11)               R10->P1 R11->P2
         MVC   RETCODE,=C'000000'           RC=0 REASON=0
         MVI   CC,X'00'
         LA    R9,RBBLK
         USING S99RB,R9
         TM    SW@ALLOC,X'80'
         BO    WRITE                       ALREADY ALLOCATED
         OI    RBPTR,X'80'
ALLOC    EQU   *                                                        00104
         OI    ALLOCLST+12,X'80'
         XC    RBBLK,RBBLK                 CLEAR RB
         MVI   S99RBLN,RBLEN               SET LENGTH
         MVI   S99VERB,S99VRBAL            SET FUNCTION
         MVC   S99TXTPP,=A(ALLOCLST)       SET PTR TO LIST
         LA    R1,RBPTR                    LOAD  RB ADDR                00104
         SVC   99                          REQ ALLOC SERVICES           00105
         LTR   R15,R15                     TEST SVC 99 RC               00107
         BNZ   SVC99ERR                                                 00108
OPENRDR  MVC   INTRDR+DCBDDNAM-IHADCB(8),DDNAME
         OPEN  (INTRDR,(OUTPUT))
         TM    INTRDR+DCBOFLGS-IHADCB,DCBOFOPN
         BNO   OPENERR
         OI    SW@ALLOC,X'80'              ALLOC & OPEN COMPLETED
WRITE    EQU   *
         PUT   INTRDR,0(R10)
*        LA    R15,PUTERR
*        ICM   R15,8,=X'02'
*        LA    R0,=X'41000000'
*        LA    R1,INTRDR
*        ICM   R1,8,=X'40'
*        BALR  R14,R15
         CLC   0(5,R10),=C'/*EOF'
         BNE   TERM
         CLOSE (INTRDR)                    FREE=CLOSE
         NI    SW@ALLOC,X'7F'              ALLOW ALLOC AGAIN
TERM     EQU   *                                                        00306
         MVC   0(6,R11),RETCODE            SET RETURN VALUES
         L     R13,SAVEAREA+4                                           00307
         L     R14,12(R13)                                              00308
         XR    R15,R15
         IC    R15,CC
         RETURN (0,12)
         BR    R14                                                      00310
         EJECT                                                          00311
SVC99ERR EQU   *
         STC   R15,CC
         CONVERT R15,RESPONSE,DWORD
         LH    R15,S99ERROR
         CONVERT R15,REASON,DWORD
         B     TERM
OPENERR  EQU   *
         MVC   RETCODE,=C'770000'
         MVI   CC,X'77'
FREE     EQU   *
         OI    FREELST,X'80'
         XC    RBBLK,RBBLK                 CLEAR RB
         MVI   S99RBLN,RBLEN               SET LENGTH
         MVI   S99VERB,S99VRBUN            SET FUNCTION
         MVC   S99TXTPP,=A(FREELST)        SET PTR TO LIST
         MVC   DDNAMX,DDNAME
         LA    R1,RBPTR                    LOAD  RB ADDR                00104
         SVC   99                          REQ ALLOC SERVICES           00105
         LTR   R15,R15                     TEST SVC 99 RC               00107
         BNZ   SVC99ERR                                                 00108
         B     TERM
PUTERR   EQU   *                           PRESERVE R13,R14 !
         SYNADAF ACSMETH=QSAM              GET SAVE
         MVI   CC,X'66'
         MVC   RETCODE,=C'660000'
         NI    SW@ALLOC,X'7F'              ALLOW ALLOC AGAIN
         CLOSE (INTRDR)                    FREE=CLOSE
         SYNADRLS                          RELEASE SAVE
         RETURN (2,12)                    EROPT=SKP (NO ABEND OF
*                                          NATURAL FOR SOFT ERRORS)
*                                                                       00312
*        DATA AREAS AND DSECTS                                          00313
*                                                                       00314
         SPACE                                                          00315
SAVEAREA DC    18F'0'                                                   00316
DWORD    DS    D
         SPACE 2                                                        00315
RETCODE  DS    0CL6
RESPONSE DS    CL2
REASON   DS    CL4
CC       DS    C
SW@ALLOC DC    X'00'
         SPACE 2                                                        00315
*++++++++++++++++++++++++++++++++++++++*
*   DYNAMIC ALLOCATION STRUCTURES      *
*++++++++++++++++++++++++++++++++++++++*
         DS    0F                                                       00348
RBPTR    DC    A(RBBLK)                ADDR OF RB HEADER                00331
         SPACE                                                          00333
         DS    0F                                                       00348
RBLEN    EQU   (S99RBEND-S99RB)
RBBLK    DC    BL(RBLEN)'0'              RB STORAGE                     00334
         SPACE 2                                                        00343
ALLOCLST DC    A(SYSOU)                TEXT POINTER LIST                00367
         DC    A(SPGNM)
         DC    A(CLOSE)
         DC    A(RTDDN)                                                 00370
         SPACE 2
FREELST  DC    A(DEALOC)               TEXT POINTER LIST                00367
         SPACE 2
         DS    0F
SYSOU    DC    AL2(DALSYSOU)            REQUEST SYSOUT ALLOCATION       00349
         DC    H'1',H'1'                                                00350
         DC    CL1'A'                   DEFAULT CLASS OF A              00351
         SPACE                                                          00352
         DS    0F
RTDDN    DC    AL2(DALRTDDN)            REQUEST GENERATED DDN           00354
         DC    H'1'                                                     00355
         DC    H'8'                                                     00356
DDNAME   DC    CL8'********'            RETURNED DDN FLD                00357
         SPACE                                                          00352
         DS    0F
SPGNM    DC    AL2(DALSPGNM)            WRITER                          00354
         DC    H'1'                                                     00355
         DC    H'8'                                                     00356
         DC    CL8'INTRDR  '            WRITER NAME                     00357
         SPACE                                                          00352
         DS    0F
CLOSE    DC    AL2(DALCLOSE)            FREE=CLOSE                      00354
         DC    H'0'                                                     00355
         SPACE                                                          00352
DEALOC   DC    AL2(DUNDDNAM)            UN-ALLOCATE                     00354
         DC    H'1'                                                     00355
         DC    H'8'                                                     00356
DDNAMX   DC    CL8'********'            WRITER NAME                     00357
         EJECT                                                          00726
INTRDR   DCB   MACRF=(PM),DSORG=PS,BLKSIZE=80,LRECL=80,                X
               RECFM=FB,SYNAD=PUTERR,EROPT=SKP
         IEFZB4D0                                                       00727
         EJECT                                                          00728
         IEFZB4D2                                                       00729
         DCBD  DSORG=PS,DEVD=DA
         END                                                            00730
