//XXXXXXXX JOB (TYYYYY,XXXX),'LEESDA',CLASS=R,REGION=4096K,
//      MSGCLASS=T,MSGLEVEL=1,NOTIFY=XXXXXXX,TIME=(,30)
//*=================================================================*//
//S1     EXEC   ASMACL,
//       PARM.ASM='XREF(SHORT),SYSPARM(NODEBUG)',
//       PARM.LKED='LET,LIST,XREF,AC(1)',COND.LKED=(5,LT)
//SYSLIB DD    BLKSIZE=32760
//       DD    DSN=DBP.MACLIB,DISP=SHR
//       DD    DSN=TST.DZL.DATA,DISP=SHR
//       DD    DSN=SYS1.MODGEN,DISP=SHR
//*      DD    DSN=SYS1.AMODGEN,DISP=SHR      CHECK DLIB VOLS!
//       DD    DSN=SYS1.AMACLIB,DISP=SHR,UNIT=SYSDA,VOL=SER=DLB001
//*      DD    DSN=SYS1.HASPSRC,DISP=SHR,
//*      UNIT=SYSDA,VOL=SER=XDLB1B
//SYSIN  DD    *
VOLLIE CSECT                            D.LEES 7/2000
VOLLIE AMODE       24
VOLLIE RMODE       24                   DCB, ACB STILL < 16M !!!
****************************************
* VOLLIE TAKES A LIST OF VOLSERS AS IN-*
* PUT AND VARIES THEM ONLINE OR OFFLINE*
* BASED ON A PARM= SETTING. VOLLIE, AS *
* IN SWITCH VOLUMES BACK AND FORTH . . *
* MUST RUN AS AN AUTHORIZED PROGRAM.   *
****************************************
* UCBSCAN WAS USED TO LOCATE ALL POTEN-*
* TIAL DEVICE ADDRESSES THEN UCBLOOK   *
* PROVIDES THE EXTENSION DATA FOR DE-  *
* DERMINING ALLOCATION STATUS. VOLLIE  *
* WILL SKIP OVER ANY DEVICE THAT IS    *
* ALLOCATED TO THE CURRENT SYSTEM.     *
****************************************
* PARM='ONLINE' OR PARM='OFFLINE'      *
* //SYSIN: VOLSER IN CC1-6             *
* //SYSOUT: SYSOUT=*                   *
* //SNAPDD: SYSOUT=* (OPTIONAL)        *
****************************************
**> R12 = BASE                         *
**> R11 = UCB COMMON                   *
**> R10 = UCB COMMON EXTENSION         *
**> R9  = INTERNAL BAL                 *
****************************************
*   D.LEES, 2000                       *
****************************************
         PRINT     NOGEN
         SAVE      (14,12)
         BALR      R12,0
         USING     *,R12
         ICM       R12,B'1000',CLEAR       WILL ALSO BE USED IN 31-BIT
         ST        R13,SAVE+4
         LA        R15,SAVE
         ST        R15,8(R13)
         LR        R13,R15
*******************
* INITIAL         *
*******************
         L         R1,0(R1)             PARM='ONLINE|OFFLINE'
         LH        R2,0(R1)
         BCTR      R2,0
         EX        R2,MVOPTN
         CLC       OPTION,ON            VALIDATE OPTION
         BE        OPTIONOK
         CLC       OPTION,OFF
         BNE       RC8
OPTIONOK EQU       *
         OPEN      (SYSIN,INPUT,SYSOUT,OUTPUT)
         AIF       ('&SYSPARM' NE 'DEBUG').NOSNAPO
SNAPINIT NOP       SNAPTERM
         OPEN      (SNAPDD,OUTPUT)
         MVI       SNAPINIT+1,X'F0'
         B         SNAPTERM
SNAPDD   DCB       DDNAME=SNAPDD,MACRF=(W),DSORG=PS,RECFM=VBA,         X
               LRECL=125,BLKSIZE=882
SNAPTERM EQU    *
.NOSNAPO ANOP
         PUT       SYSOUT,HEADING
         MODESET   KEY=ZERO,MODE=SUP    MODESET!
         EJECT
*******************
* MAINLINE        *
*******************
GETVOL   EQU       *
         GET       SYSIN,VOLSER
         XC        WORKAREA,WORKAREA    RESET WORKAREA F/NEW VOLSER
         LA        R1,SETA31            31-BIT TO ALLOW FOR HIGH UCBS
         O         R1,MAKE31
         BSM       0,R1
SETA31   EQU       *
UCBSCAN  EQU       *                     SEARCH FOR VOLSER MATCH
         UCBSCAN   ADDRESS,                                            C
               WORKAREA=WORKAREA,                                      C
               DEVCLASS=DASD,                                          C
               UCBPTR=UCBPTR,                                          C
               DYNAMIC=YES,                                            C
               LOC=ANY,                                                C
               RANGE=ALL,                                              C
               NOPIN
         LTR       R15,R15              Q. GOT A UCB?
         BNZ       RC4                  SCANNED ALL UCBS + NO JOY
         L         R11,UCBPTR
         USING     UCBOB,R11             ADDRESS UCB TO TEST VOLSER
         CLC       UCBVOLI,VOLSER
         BE        FOUND                 FOUND VOLSER MATCH
         B         UCBSCAN               NO MATCH / TRY NEXT UCB
FOUND    EQU       *                     PROCESS VOLSER
         AIF       ('&SYSPARM' NE 'DEBUG').NOSNAP1
         MVC       DEBUG2,UCBCHAN
         SNAP DCB=SNAPDD,PDATA=REGS,STORAGE=(AA,ZZ),ID=1
.NOSNAP1 ANOP
         UCBLOOK   VOLSER=VOLSER,                                      C
               DEVCLASS=DASD,                                          C
               UCBPTR=UCBPTR,                                          C
               UCBCXPTR=UCBCXPTR,                                      C
               DYNAMIC=YES,                                            C
               LOC=ANY,                                                C
               RANGE=ALL,                                              C
               PIN,                                                    C
               TEXT=PTEXT,                                             C
               PTOKEN=PTOKEN
         LTR       R15,R15              Q. GOT A UCB?
         BNZ       RC12                 SCAN FOUND / LOOK FAILED . .
ALLOC    EQU       *
         L         R11,UCBPTR
         USING     UCBOB,R11            MAP COMMON
         L         R10,UCBCXPTR
         USING     UCBCMEXT,R10         MAP COMMON EXTENSION
         AIF       ('&SYSPARM' NE 'DEBUG').NOSNAP2
         MVC       DEBUG1,UCBASID
         SNAP DCB=SNAPDD,PDATA=REGS,STORAGE=(AA,ZZ),ID=2
.NOSNAP2 ANOP
         TM        UCBSTAT,UCBALOC      DEVICE ALLOCATED?
         BNO       SET                  NO
         CLC       UCBASID,CLEAR        ALLOC ON THIS SYSTEM?
         BE        SET                  NO, GO AHEAD AND SET STATUS
         UCBPIN    UNPIN,PTOKEN=PTOKEN  DO NOT PROCESS; > NEXT
         B         RC4
SET      EQU       *
         CLC       OPTION,ON
         BNE       VARYOFF
VARYON   EQU       *
         OI        UCBSTAT,UCBONLI      SET VARY FLAG TO ON
         NI        UCBFLA,BOXOFF        SET BOXED FLAG OFF
         B         UNPIN
VARYOFF  EQU       *
         NI        UCBSTAT,UCBOFFLI     SET VARY FLAG TO OFF
         NI        UCBFLA,BOXOFF        SET BOXED FLAG OFF
UNPIN    EQU       *
         UCBPIN    UNPIN,PTOKEN=PTOKEN  UNPIN (EOJ WILL CLEANUP)
POSTIT   EQU       *
         MVC       MESSAGE,MSGSKEL1
         MVC       MESSAGE(6),VOLSER
         BAL       R9,CVTUNIT           RETURNS UNIT FIELD
         MVC       MESSAGE+7(4),UNIT
         MVC       MESSAGE+19(7),OPTION
         LA        R1,SETA24
         N         R1,MAKE24
         BSM       R0,R1
SETA24   EQU       *
         PUT       SYSOUT,MESSAGE
         B         GETVOL
*******************
* RETURN          *
*******************
RETURN   EQU       *
         MODESET   KEY=NZERO,MODE=PROB    /*PREVENT S30A*/
         PUT       SYSOUT,TRAILER
         CLOSE     (SYSIN,,SYSOUT)
         AIF       ('&SYSPARM' NE 'DEBUG').NOSNAPC
         CLOSE     (SNAPDD)
.NOSNAPC ANOP
         L         R13,SAVE+4
         L         R15,RETCODE
         RETURN    (14,12),RC=(15)
RC4      EQU       *
         MVC       MESSAGE,MSGSKEL1
         MVC       MESSAGE(6),VOLSER
         BAL       R9,CVTUNIT
         MVC       MESSAGE+7(4),UNIT
         MVC       MESSAGE+19(6),FAILED
         PUT       SYSOUT,MESSAGE
         LA        R0,4
         ST        R0,RETCODE
         B         GETVOL
RC8      EQU       *
         WTO       '++++ UNRECOGNIZED PARAMETER ++++',ROUTCDE=11
         LA        R15,8
         ST        R15,RETCODE
         B         RETURN
RC12     EQU       *
         WTO       '++++ UTTER AND COMPLETE FAILURE ++++',ROUTCDE=11
         LA        R15,12
         ST        R15,RETCODE
         B         RETURN
*******************
* CVTUNIT         *
*******************
CVTUNIT  EQU       *  ONE OF MANY, MANY WAYS . . . .
         ST        R9,CALLSAVE
         SR        R2,R2
         SR        R3,R3
         LA        R8,UCBCHAN
         IC        R2,0(R8)   WHERE RX -> 1 BYTE OF HEX (2 CHARACTERS)
         SRDL      R2,4 SHIFT +4 BITS TO THE RIGHT (1 NIBBLE INTO R3)
         SRL       R3,28 SHIFT +28 BITS TO THE RIGHT (NIBBLE SHIFT)
         STC       R2,UNIT+0
         STC       R3,UNIT+1
         SR        R2,R2
         SR        R3,R3
         IC        R2,1(R8)   WHERE RX -> 1 BYTE OF HEX (2 CHARACTERS)
         SRDL      R2,4 SHIFT +4 BITS TO THE RIGHT (1 NIBBLE INTO R3)
         SRL       R3,28 SHIFT +28 BITS TO THE RIGHT (NIBBLE SHIFT)
         STC       R2,UNIT+2
         STC       R3,UNIT+3
         TR        UNIT(4),TRTBL
         L         R9,CALLSAVE
         BR        R9
*******************
* STORAGE         *
*******************
AA       EQU   *
CALLSAVE DS    F
MVOPTN   MVC   OPTION(0),2(R1)
OPTION   DC    CL7' '                                                -
PTOKEN   DS    CL8
RETCODE  DC    F'0'
SAVE     DS    9D
TARGET   DS    CL4
UCBPTR   DS    A
UCBCXPTR DS    A
UNIT     DS    CL4
WORKAREA DC    XL100'00'                          UCBSCAN WORKA.
DEBUG1   DS    F
DEBUG2   DS    F
ZZ       EQU   *
*******************
* CONSTANTS       *
*******************
CLEAR    DC    A(0)
FAILED   DC    C'FAILED'
HEADING  DC    CL80'THE VOLLIE REPORT'
         DS    0F                                  ALIGN MASKS
MAKE24   DC    X'7FFFFFFF'
MAKE31   DC    X'80000000'
MSGSKEL1 DC    CL80'______ ____ VARIED ______'
OFF      DC    CL7'OFFLINE'
ON       DC    CL7'ONLINE'
PTEXT    DC    CL58'PINNED BY THE VOLLIE PROGRAM'
TRAILER  DC    CL80'GAME, SET, AND MATCH'
TRTBL    DC    C'0123456789ABCDEF'
*******************
* DCBS / BUFFERS  *
*******************
SYSIN    DCB       DDNAME=SYSIN,MACRF=(GM),LRECL=80,                   +
               EODAD=RETURN,DSORG=PS
VOLSER   DS    CL6,CL74
*
SYSOUT   DCB       DDNAME=SYSOUT,MACRF=(PM),RECFM=FB,                  +
               LRECL=80,BLKSIZE=0,DSORG=PS
MESSAGE  DC    CL80' '
*******************
* DSECTS          *
*******************
         PRINT     NOGEN
         IEFUCBOB  PREFIX=YES
*******************
* EQUATES         *
*******************
BOXOFF   EQU      B'11111110'
UCBOFFLI EQU      B'01111111'
         REGS
         END
//LKED.SYSLMOD  DD DSN=TST.DZL.XMIT.LOAD(VOLLIE),DISP=SHR
//LKED.SYSOUT DD SYSOUT=*
